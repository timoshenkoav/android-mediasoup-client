apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
// Source of webrtc.
def WEBRTC_INC_PATH = "${projectDir}/deps/webrtc/src/"
// Precompiled static libs and jar for webrtc android.
//def WEBRTC_BASE_PATH = "${projectDir}/deps/webrtc/lib/"
def WEBRTC_LIB_PATH = "${projectDir}/deps/webrtc/lib/"

android {
    compileSdkVersion 32

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 308
        versionName "3.0.8"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                // Passes optional arguments to CMake.
                arguments "-DLIBWEBRTC_INCLUDE_PATH=" + WEBRTC_INC_PATH,
                        "-DLIBWEBRTC_BINARY_PATH=" + WEBRTC_LIB_PATH,
                        "-DMEDIASOUPCLIENT_BUILD_TESTS=false",
                        "-DSDPTRANSFORM_BUILD_TESTS=OFF",
                        "-DSDPTRANSFORM_BUILD_READMEHELPER=OFF",
                        "-DMEDIASOUPCLIENT_LOG_TRACE=ON",
                        "-DMEDIASOUPCLIENT_LOG_DEV=ON",
                        "-DANDROID_STL=c++_static"
                cppFlags "-Wl,--build-id -g"
            }
        }

        ndk {
            // Specifies the ABI configurations of your native
            // libraries Gradle should build and package with your APK.
            abiFilters 'x86' ,'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        doNotStrip '**.so'     // ADD THIS! #1
    }
    externalNativeBuild {
        cmake {
            // Provides a relative path to your CMake build script.
            path "CMakeLists.txt"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api fileTree(dir: WEBRTC_LIB_PATH, include: ['libwebrtc.jar'])

    implementation 'androidx.appcompat:appcompat:1.4.2'
//    testImplementation 'junit:junit:4.13'
//    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
//    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                version = System.getenv("VERSION") ?: "0"
                group = System.getenv("GROUP") ?: "com.mediasoup"
                artifactId = 'android'
            }
        }
    }
}